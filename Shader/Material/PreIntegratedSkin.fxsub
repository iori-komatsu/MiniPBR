#ifndef SHADER_MATERIAL_PREINTEGRATED_SKIN_INCLUDED
#define SHADER_MATERIAL_PREINTEGRATED_SKIN_INCLUDED

#include <Shader/Material/Base.fxsub>
#include <Shader/BRDF/Specular.fxsub>
#include <Shader/BRDF/OrenNayar.fxsub>
#include <Shader/ShadowMap/Sampling.fxsub>
#include <Shader/Curvature.fxsub>

float _SpecularSmoothness            : CONTROLOBJECT < string name=CONTROLLER_NAME; string item="îΩéÀåıÇÃääÇÁÇ©Ç≥"; >;
float _DiffuseSmoothness             : CONTROLOBJECT < string name=CONTROLLER_NAME; string item="ägéUåıÇÃääÇÁÇ©Ç≥"; >;
float _ReduceShadow                  : CONTROLOBJECT < string name=CONTROLLER_NAME; string item="âeÇîñÇﬂÇÈ"; >;
float _SubsurfaceScatteringScale     : CONTROLOBJECT < string name=CONTROLLER_NAME; string item="îÁâ∫éUóê"; >;
float _SubsurfaceScatteringScaleX100 : CONTROLOBJECT < string name=CONTROLLER_NAME; string item="îÁâ∫éUóêx100"; >;

static const float SpecularRoughness = 1.0 - _SpecularSmoothness;
static const float DiffuseRoughness  = 1.0 - _DiffuseSmoothness;
static const float MinLightVisibility = _ReduceShadow;
static const float SubsurfaceScatteringScale = lerp(1.0, 10.0, _SubsurfaceScatteringScale) * lerp(1.0, 100.0, _SubsurfaceScatteringScaleX100);

texture2D PreIntegratedSkinLUT <
    string ResourceName = PRE_INTEGRATED_SKIN_LUT_PATH;
>;
sampler2D PreIntegratedSkinLUTSamp = sampler_state {
    Texture = <PreIntegratedSkinLUT>;
    MinFilter = LINEAR;
    MagFilter = LINEAR;
    MipFilter = NONE;
    AddressU = CLAMP;
    AddressV = CLAMP;
};

float3 SubsurfaceScattering(float dotNL, float3 worldPos, float3 normal) {
    float u = Remap11To01(dotNL);
    float v = 1.0 - saturate(Curvature(worldPos, normal) * SubsurfaceScatteringScale);
    return tex2D(PreIntegratedSkinLUTSamp, float2(u, v)).rgb;
}

float3 ShaderSurface(
    float3 worldPos,
    float3 baseColor,
    float3 normal,
    float3 viewDir,
    float3 lightDir,
    float3 lightIrradiance,
    float  normalLength,
    uniform bool selfShadow
) {
    float lightVisibility;
    if (selfShadow) {
        float s = CastShadow(worldPos, dot(normal, lightDir));
        lightVisibility = lerp(MinLightVisibility, 1.0, s);
    } else {
        lightVisibility = 1;
    }

    float3 h = normalize(viewDir + lightDir);
    float dotNL = dot(normal, lightDir);
    float dotNV = saturate(dot(normal, viewDir)); // NÅEV < 0 ÇÕåvéZåÎç∑Ç≈ÇµÇ©Ç†ÇËÇ¶Ç»Ç¢ÇÃÇ≈ saturate Ç∑ÇÈ
    float dotNH = dot(normal, h);
    float dotVH = dot(viewDir, h);
    float dotLH = dot(lightDir, h);
    float dotLV = dot(lightDir, viewDir);

    const float f0 = 0.028; // "ÉäÉAÉãÉ^ÉCÉÄÉåÉìÉ_ÉäÉìÉO ëÊ4î≈" 283ÉyÅ[ÉWÇÃï\ÇÊÇË
    float roughness = GetToksvigRoughness(pow2(SpecularRoughness), normalLength);
    float3 fSpecular = SpecularBRDF(dotNL, dotNV, dotNH, dotVH, roughness, f0) * saturate(dotNL);

    float3 scatterCoeff = SubsurfaceScattering(dotNL, worldPos, normal);
    // scatterCoeff ÇÇªÇÃÇ‹Ç‹égÇ§Ç∆ã»ó¶Ç™çÇÇ¢Ç∆Ç´Ç…îßÇ™ê¬Ç¡Ç€Ç≠Ç»Ç¡ÇƒÇµÇ‹Ç§ÇÃÇ≈âûã}èàíuÇ∆ÇµÇƒ dotNL Ç∆ max ÇéÊÇ¡ÇƒÇ®Ç≠
    scatterCoeff = max(scatterCoeff, dotNL);
    float3 fDiffuse = OrenNayarBurleyBRDF(
        scatterCoeff, // N•L ÇÃë„ÇÌÇËÇ… scatterCoeff ÇégÇ§
        dotNV, dotLV, dotLH,
        baseColor,
        DiffuseRoughness) * scatterCoeff;

    return (fSpecular + fDiffuse) * lightIrradiance * lightVisibility
         + AmbientIrradiance * baseColor;
}

#endif
